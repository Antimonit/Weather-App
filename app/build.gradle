plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]

        applicationId globalConfiguration["applicationId"]
        versionCode globalConfiguration["versionCode"]
        versionName globalConfiguration["versionName"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "API_KEY", "\""+localProperties['apiKey']+"\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_KEY", "\""+localProperties['apiKey']+"\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    def appDependencies = rootProject.ext.appDependencies
    def appTestDependencies = rootProject.ext.appTestDependencies

    implementation appDependencies.kotlin
    implementation appDependencies.appCompat
    implementation appDependencies.constraintLayout
    implementation appDependencies.ktx
    implementation appDependencies.timber
    implementation appDependencies.dagger
    implementation appDependencies.daggerSupport
    implementation appDependencies.rxKotlin
    implementation appDependencies.rxAndroid
    implementation appDependencies.gson
    implementation appDependencies.material
    implementation appDependencies.retrofit
    implementation appDependencies.retrofitAdapter
    implementation appDependencies.retrofitConverter
    implementation appDependencies.okHttp
    implementation appDependencies.okHttpLogger
    implementation appDependencies.swipeRefreshLayout
    implementation appDependencies.navigationFragment
    implementation appDependencies.navigationUi

    debugImplementation appDependencies.chuckerDebug
    releaseImplementation appDependencies.chuckerRelease

    kapt appDependencies.daggerCompiler
    kapt appDependencies.daggerProcessor

    testImplementation appTestDependencies.junit
    androidTestImplementation appTestDependencies.junitAndroid
    androidTestImplementation appTestDependencies.espressoCore

    implementation fileTree(dir: 'libs', include: ['*.jar'])
}